plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.3'
}

version = '0.3.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'DAQiFi Java API & Emulator',
                'Implementation-Version': version,
                'Main-Class': 'com.daqifi.common.devices.Emulator'
    }
    archiveBaseName.set(project.name)
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

build.finalizedBy(fatJar)

repositories {
    mavenCentral()
    google()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.4'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite' // Enable Java Lite generation
                }
            }
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.implementation
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:4.29.2'
}
