apply plugin: 'java'
apply plugin: 'com.google.protobuf'

version = '0.1.1'
sourceCompatibility = 1.7
targetCompatibility = 1.7


//create a single Jar with all dependencies
task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'DAQiFi Java API & Emulator',
        	'Implementation-Version': version,
        	'Main-Class': 'com.daqifi.common.devices.Emulator'
    }
    archiveBaseName.set(project.name)
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

build.finalizedBy(fatJar)

//Get dependencies from Maven central repository
repositories {
    mavenCentral()
    google()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.implementation
}

//Project dependencies
dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
  }
}
